################################################################################
#
# RevBayes Example: Bayesian inference of speciation rate (Yule/pure birth model)
# 
# This file: Runs the full MCMC on a fixed tree.
#
# authors: Sebastian Hoehna
#
################################################################################

#######################
# Reading in the Data #
#######################

###### This just defines a single model for all sites #######

### Read in sequence data for both genes

data <- readTrees("data/primates.tree")[1]

# Get some useful variables from the data. We need these later on.
n_species <- data.ntips()
names <- data.names()

# set my move index
mi = 0



##############
# Tree model #
##############

# Specify a prior on the diversification and turnover rate
diversification ~ dnUniform(0,1000)
relativeExtinction ~ dnBeta(1,1)

#diversification ~ dnUniform(0,100)
#relativeExtinction ~ dnUniform(0,10)

# now transform the diversification and turnover rates into speciation and extinction rates
speciation := abs(diversification / (1.0 - relativeExtinction) )
extinction := speciation * relativeExtinction
#speciation := diversification + relativeExtinction
#extinction := relativeExtinction


# specify a prior on the root age (our informed guess is about 75-80 mya)
root <- data.rootAge()

sampling_fraction <- 23 / 450 # 23 out of the ~ 450 primate species

# create some moves that change the stochastic variables
# all moves are sliding proposals but you could use scaling proposals for the rates too
moves[++mi] = mvSlide(diversification,delta=1,tune=true,weight=2)
moves[++mi] = mvSlide(relativeExtinction,delta=1,tune=true,weight=2)
moves[++mi] = mvScale(diversification,lambda=1,tune=true,weight=2)
moves[++mi] = mvScale(relativeExtinction,lambda=1,tune=true,weight=2)

# construct a variable for the tree drawn from a birth death process
psi ~ dnBDP(lambda=speciation, mu=extinction, rootAge=root, rho=sampling_fraction, nTaxa=n_species, names=names )

# attach the data
psi.clamp(data)




#############
# THE Model #
#############

# We define our model.
# We can use any node of our model as a handle, here we chose to use the rate matrix.
mymodel = model(speciation)






monitors[1] = mnModel(filename="output/BirthDeathUniformSampling.log",printgen=10, separator = TAB)
monitors[2] = mnScreen(printgen=1000, speciation, extinction)

mymcmc = mcmc(mymodel, monitors, moves)

mymcmc.burnin(generations=10000,tuningInterval=250)
mymcmc.run(generations=100000)


# you may want to quit RevBayes now
#q()

